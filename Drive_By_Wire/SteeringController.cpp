#include <Arduino.h>
#include "PID.h"
#include "DBW_Pins.h"
#include "Settings.h"
#include "SteeringController.h"

/*-----------------------------------------------------------------------------------*/
SteeringController::SteeringController(Startup startup)
  : steerAngle_DegX10(0), SteerControl(0), desiredTurn_DegX10(0),
    steerPID(&steerAngle_DegX10, &SteerControl, &desiredTurn_DegX10,
             proportional_steering, integral_steering, derivative_steering, DIRECT) {
  if (startup == Startup::Diagnostic) {
    analogWrite(STEER_SPEED_PIN, 0);
    digitalWrite(STEER_ON_PIN, ST_OFF);
    digitalWrite(STEER_PULSE_PIN, LOW);
    digitalWrite(LEFT_TURN_PIN, LOW);
    pinMode(STEER_SPEED_PIN, OUTPUT);
    pinMode(STEER_ON_PIN, OUTPUT);
    pinMode(STEER_DIR_PIN, OUTPUT);
    pinMode(STEER_PULSE_PIN, OUTPUT);
    pinMode(LEFT_TURN_PIN, OUTPUT);
    stopTest();
    return;
  }
switch (STEER_METHOD)
{
case STR_HBRIDGE:
  // TWO-WIRE DIGITAL STEERING:
  //   LEFT_TURN_PIN  : HIGH = drive steer motor LEFT
  //   RIGHT_TURN_PIN : HIGH = drive steer motor RIGHT
  //   (see DBW_Pins.h for which physical pins those are on this board - they
  //   are not D26/D28 here, as an earlier version of this comment claimed)
  //   both LOW = HOLD. Router uses digitalRead (single PIO register access,
  //   robust against CAN-interrupt contention) and dispatches into
  //   updateAngle(lTurn,rTurn). DBW maintains its own open-loop model of
  //   Router's angle_tenths (see update()) since the L_SENSE/R_SENSE wires
  //   for analog feedback don't carry signal on this bridge.
    pinMode(LEFT_TURN_PIN,  OUTPUT);
    pinMode(RIGHT_TURN_PIN, OUTPUT);
    digitalWrite(LEFT_TURN_PIN,  LOW);
    digitalWrite(RIGHT_TURN_PIN, LOW);
    break;

  case STR_PWM:
  // To do: Send STEER_PULSE_PIN high, and set a timing interrupt to take it low at 1500 us. to center the steering
  // PID controller is not used. 
    steerPID.SetControlLimits(MIN_LEFT_US, MAX_RIGHT_US);
    break;

  case STR_MOTOR_CONTROL:
    // for motor control board
    pinMode(STEER_SPEED_PIN,OUTPUT); // PWM A  
    pinMode(STEER_ON_PIN,OUTPUT);    // Brake A
    pinMode(STEER_DIR_PIN,OUTPUT);   // Dir A
    digitalWrite(STEER_ON_PIN, ST_OFF);
    analogWrite(STEER_SPEED_PIN, 100);   // slowr speed for initial centering
    // analogWrite(STEER_SPEED_PIN, 255);     // fast steer control
  }

  steerPID.SetSampleTime(PID_SAMPLE_TIME);
  steerPID.SetMode(AUTOMATIC);
   
  update(0);    // point wheels straight ahead
 
   if (DEBUG) {
    Serial.println("Steering Setup Complete");
  }
}
/*-----------------------------------------------------------------------------------*/
SteeringController::~SteeringController() {}
/*-----------------------------------------------------------------------------------*/
int SteeringController::update(int desiredangle_DegX10, int measured_angle_DegX10)
{
  // Closed-loop steering. Preferred source for the actual wheel angle is
  // measured_angle_DegX10 passed in by Vehicle — fed by the simulator's
  // 0x430 CAN frame (or, on real hardware, by an L_SENSE/R_SENSE analog
  // read done in Vehicle::update before the call).
  //
  // If no measured value has ever arrived (haveMeasured stays false), fall
  // back to an open-loop integrator that integrates whatever we drove last
  // loop. This keeps DBW operational in degraded environments — sim with
  // no 0x430 publisher, or real trike whose sensors are absent.
  if (measured_angle_DegX10 != 0) haveMeasured = true;

  if (haveMeasured) {
    steerAngle_DegX10 = (float)measured_angle_DegX10;
  } else {
    // Open-loop fallback: mirror Router's fixed 20 degX10 / loop step
    // (ANGLE_CHANGE_TENTHS in simulator_stage1.ino). One step per DBW
    // loop while a direction wire is asserted.
    const int ANGLE_STEP_DegX10 = 20;
    const int MAX_ANGLE_DegX10  = 250;
    if (drovLeftLast)  steerAngle_DegX10 -= ANGLE_STEP_DegX10;
    if (drovRightLast) steerAngle_DegX10 += ANGLE_STEP_DegX10;
    if (steerAngle_DegX10 >  MAX_ANGLE_DegX10) steerAngle_DegX10 =  MAX_ANGLE_DegX10;
    if (steerAngle_DegX10 < -MAX_ANGLE_DegX10) steerAngle_DegX10 = -MAX_ANGLE_DegX10;
  }
  currentAngle_DegX10 = (int)steerAngle_DegX10;

  SteeringPID(desiredangle_DegX10);
  delay(1);
  return (int)steerAngle_DegX10;
}
/*-----------------------------------------------------------------------------------*/
void SteeringController::SteeringPID(int input_DegX10) {
  desiredTurn_DegX10 = input_DegX10;
  // Bang-bang on two digital wires with deadband.
  //   err > +10  -> RIGHT_TURN HIGH, LEFT_TURN LOW
  //   err < -10  -> LEFT_TURN HIGH,  RIGHT_TURN LOW
  //   |err| <=10 -> both LOW (hold)
  // update() advances the modeled angle next loop based on which direction
  // we drove this loop.
  // Deadband is 10 (= half the 20-tenth/loop slew step). The wheel can only
  // rest on a 20-spaced grid, so the nearest holdable point is at most 10 away.
  // A smaller deadband (e.g. 5) leaves the wheel unable to land inside the band
  // and it hunts +/-10 forever; 10 lets it settle.
  const int DEADBAND_DegX10 = 10;
  int err = input_DegX10 - (int)steerAngle_DegX10;
  bool turnLeft  = (err < -DEADBAND_DegX10);
  bool turnRight = (err >  DEADBAND_DegX10);
#if (STEER_METHOD == STR_MOTOR_CONTROL)
  if (err >= -DEADBAND_DegX10 && err <=  DEADBAND_DegX10)
  {
    digitalWrite(STEER_ON_PIN, ST_OFF);  // no movement
  }
  else {
   // analogWrite, not digitalWrite: STEER_SPEED_PIN is the shield's PWM A pin,
   // and digitalWrite(pin, 0xFF) is just HIGH - full speed with no way to do
   // anything else, which made the "slow down for small err" note below
   // impossible to act on. 255 is the same full speed, but now scalable.
   // To do: read motor current on A0 and redeuce speed if too much power.
   // NOTE: this writes the raw bool, so turning right drives the pin HIGH -
   // which contradicts ST_RIGHT (= !ST_LEFT = LOW) in DBW_Pins.h. One of the
   // two is wrong. Left as-is deliberately: picking a side without a meter on
   // the motor controller could send the steering the wrong way. Verify on
   // hardware before running STR_MOTOR_CONTROL, then use ST_LEFT/ST_RIGHT here.
  driveMotor(255, turnRight);
  }
#elif (STEER_METHOD == STR_HBRIDGE)
  digitalWrite(LEFT_TURN_PIN,  turnLeft  ? HIGH : LOW);
  digitalWrite(RIGHT_TURN_PIN, turnRight ? HIGH : LOW);
#else  // pulse width
  // To do: Find servo destination and write high to STEER_PULSE_PIN. 
  // Set a timer to take pulse low according to desired pulse width.
#endif
  drovLeftLast  = turnLeft;
  drovRightLast = turnRight;

  static uint32_t lastDbg_ms = 0;
  // Only print when the USB TX buffer has room — otherwise SerialUSB.print()
  // blocks on a full buffer when no host is reading and stalls the control
  // loop. availableForWrite() gating makes the print non-blocking: it is simply
  // skipped when there is no room.
  if (SerialUSB && SerialUSB.availableForWrite() >= 96 && millis() - lastDbg_ms > 1000) {
    lastDbg_ms = millis();
    SerialUSB.print("# DBW steer cmd="); SerialUSB.print(input_DegX10);
    SerialUSB.print(" modeled=");        SerialUSB.print((int)steerAngle_DegX10);
    SerialUSB.print(" err=");            SerialUSB.print(err);
    SerialUSB.print(" L=");              SerialUSB.print(turnLeft  ? 1 : 0);
    SerialUSB.print(" R=");              SerialUSB.println(turnRight ? 1 : 0);
  }
}
/*-----------------------------------------------------------------------------------*/
// Get data from sensor on left steering column.
int SteeringController::computeAngleLeft() {
  int val = readLeftSensorRaw();
  int degreeX10;
  if (val == Left_Straight_Read) 
     {  currentAngle_DegX10 = 0; return 0;}
  if (val > Left_Straight_Read)
  {  // right turn
     degreeX10 = (val - Left_Straight_Read) * (MIN_LEFT_DEGx10) / (Left_Straight_Read-Left_Read_at_MIN_TURN);
  }
  else
  {  // left turn
     degreeX10 = -(val - Left_Straight_Read) * (MAX_RIGHT_DEGx10) / (Left_Straight_Read-Left_Read_at_MAX_TURN);
  }
  if (DEBUG) {
    Serial.print("Left sensor: ");
    Serial.print(degreeX10);
    Serial.print(", ");
    Serial.println(val);
  }
  currentAngle_DegX10 = degreeX10;
  return degreeX10;
}
/*-----------------------------------------------------------------------------------*/
// Get data from sensor on right steering column.
int SteeringController::computeAngleRight() {
  int val = analogRead(R_SENSE_PIN);
  int degreeX10;  
  if (val == Right_Straight_Read) 
     return 0;
  if (val > Right_Straight_Read)
  {  // right turn
     degreeX10 = (val - Right_Straight_Read) * (MIN_LEFT_DEGx10) / (Right_Straight_Read-Right_Read_at_MIN_TURN);
  }
  else
  {  // left turn
     degreeX10 = -(val - Right_Straight_Read) * (MAX_RIGHT_DEGx10) / (Right_Straight_Read-Right_Read_at_MAX_TURN);
  }
  if (DEBUG) {
    Serial.print("Right sensor: ");
    Serial.print(degreeX10);
    Serial.print(", ");
    Serial.println(val);
  }
  return degreeX10;  
}
/*-----------------------------------------------------------------------------------*/
int SteeringController::getSteeringMode() {
  return steeringMode;
}

int SteeringController::readLeftSensorRaw() const {
  return analogRead(L_SENSE_PIN);
}

void SteeringController::driveMotor(uint8_t pwm, uint8_t direction) {
  analogWrite(STEER_SPEED_PIN, pwm);
  digitalWrite(STEER_DIR_PIN, direction);
  digitalWrite(STEER_ON_PIN, ST_ON);
}

void SteeringController::testMotor(bool turnLeft, uint8_t pwm) {
  stopTest();
  driveMotor(pwm, turnLeft ? ST_LEFT : ST_RIGHT);
}

bool SteeringController::testPulse(uint16_t width_us) {
  if (width_us < MIN_LEFT_US || width_us > MAX_RIGHT_US) return false;
  stopTest();
  Steer_Servo.attach(STEER_PULSE_PIN, MIN_LEFT_US, MAX_RIGHT_US);
  Steer_Servo.writeMicroseconds(width_us);
  currentSteering_us = width_us;
  return true;
}

void SteeringController::stopTest() {
  analogWrite(STEER_SPEED_PIN, 0);
  digitalWrite(STEER_ON_PIN, ST_OFF);
  if (Steer_Servo.attached()) Steer_Servo.detach();
  digitalWrite(STEER_PULSE_PIN, LOW);
  digitalWrite(LEFT_TURN_PIN, LOW);
  currentSteering_us = 0;
}

/*-----------------------------------------------------------------------------------*/
// Move the wheels to center, then left, right, center.
// Use left sensor for feedback.
void SteeringController::test() {
  computeAngleLeft();  // left turn if currentAngle_DegX10 < 0
  switch (STEER_METHOD)
  {
  case STR_HBRIDGE:
  // TWO-WIRE DIGITAL STEERING:
    digitalWrite(LEFT_TURN_PIN,  LOW);
    digitalWrite(RIGHT_TURN_PIN, LOW);
   if (currentAngle_DegX10 < 0)
      while (computeAngleLeft() < 0)
        digitalWrite(RIGHT_TURN_PIN, HIGH);  // move to center
    else  
     while (computeAngleLeft() > 0)
        digitalWrite(LEFT_TURN_PIN, HIGH);  // move to center

    // continue to move to sides and back. No one is currently using this steering modde.
    break;

  case STR_PWM:
  // To do: Send STEER_PULSE_PIN high, and set a timing interrupt to take it low at 1500 us. to center the steering
  // then MIN_LEFT_US  for left, MAX_RIGHT_U for right, 1500 to center.
    break;

  case STR_MOTOR_CONTROL:
   // for motor control board
    analogWrite(STEER_SPEED_PIN, 100);   // slowr speed for initial centering
    digitalWrite (STEER_DIR_PIN, computeAngleLeft() < 0 ? ST_LEFT: ST_RIGHT);
    if (currentAngle_DegX10 < 0)
    {
      digitalWrite (STEER_ON_PIN, ST_ON);
      while (computeAngleLeft() < 0) ;
      digitalWrite (STEER_ON_PIN, ST_OFF);
    }
    digitalWrite (STEER_DIR_PIN, ST_RIGHT);
    if (currentAngle_DegX10 > 0)
    {
      digitalWrite (STEER_ON_PIN, ST_ON);
      while (computeAngleLeft() > 0) ;
      digitalWrite (STEER_ON_PIN, ST_OFF);
    }
    // centered. Now pause and move right.
    digitalWrite (STEER_ON_PIN, ST_OFF);
    delay(1000);
    analogWrite(STEER_SPEED_PIN, 170);     // faster steer control
    digitalWrite (STEER_ON_PIN, ST_ON);
    while (computeAngleLeft() <  MAX_RIGHT_DEGx10) ;
    digitalWrite (STEER_ON_PIN, ST_OFF);
    delay(1000);

    // move left
    digitalWrite (STEER_DIR_PIN, ST_LEFT);
    digitalWrite (STEER_ON_PIN, ST_ON);
    while (computeAngleLeft() > MIN_LEFT_DEGx10) ;
    digitalWrite (STEER_ON_PIN, ST_OFF);
    delay(1000);
    // back to center
    digitalWrite (STEER_DIR_PIN, ST_RIGHT);
    digitalWrite (STEER_ON_PIN, ST_ON);
    while (computeAngleLeft() < 0) ;
    digitalWrite (STEER_ON_PIN, ST_OFF);
    }
  }


