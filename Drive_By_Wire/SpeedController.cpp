#include <Arduino.h>
#include "DBW_Pins.h"
#include "SpeedController.h"

volatile uint32_t SpeedController::tickTime_ms[2];

SpeedController::SpeedController(Startup startup)
  : speedPID(&speedCyclometer_cmPs, &PIDThrottle, &desiredSpeed_cmPs, proportional_throttle, integral_throttle, derivative_throttle, DIRECT) 
{
   // Initialize Pins
  if (startup == Startup::Diagnostic) {
    digitalWrite(BRAKE_ON_PIN, ON_BR);
    digitalWrite(BRAKE_VOLT_PIN, ON_BR);
  }
  pinMode(BRAKE_ON_PIN, OUTPUT);
  pinMode(BRAKE_VOLT_PIN, OUTPUT);
  // Brakes are released as a default setting
  state = BR_OFF;
  brake_change_ms = 0;  // only used when brakes on
  if (startup == Startup::Diagnostic) Stop();
  else ReleaseBrakes();
  
  currentThrottle = 0;
  speedPID.SetControlLimits(MIN_PID_TH, MAX_PID_TH);
  speedPID.SetSampleTime(PID_SAMPLE_TIME);
  speedPID.SetMode(startup == Startup::Normal ? AUTOMATIC : MANUAL);
  calcTime_ms[0] = 0;
  calcTime_ms[1] = 0;
  prevSpeed_cmPs = 0;

  if (startup == Startup::Normal) attachInterrupt(IRPT_WHEEL, tick, RISING);
  if (DEBUG)
    Serial.println("Speed Setup Complete");
}

SpeedController::~SpeedController() {
}

// Interrupt service routine
void SpeedController::tick() {
  uint32_t tick = millis();
  noInterrupts();
  if ((tick - tickTime_ms[0]) > MIN_TICK_TIME_ms) {
    tickTime_ms[1] = tickTime_ms[0];
    tickTime_ms[0] = tick;
  }
  interrupts();
}

/**
 * receives requested speed from Vehicle requested from High Level board
 * attempts to adjust speed either through PIDS or standard engageThrottle() 
 * based on the PIDs being on or off in Settings
 * param dSpeed desired speed in mm/s
 */
int32_t SpeedController::update(int32_t dSpeed, DriveMode mode) {
  serviceBrakes();

  // If Nav (or the operator path) commands zero speed, bypass the PID and
  // brake hard. The PID's integral accumulates a positive bias during the
  // cruise phase; if we keep running PID(0) afterward, the integral keeps
  // DAC0 above zero and the ACCEL branch in ThrottlePID() releases the
  // brakes — so the simulator keeps reading "throttle on" and the trike
  // never actually stops. Same wind-up condition that would occur on the
  // real trike after a long cruise.
  if (dSpeed <= 0) {
    Stop();
    computeSpeed();
    return 0;
  }

  // Kelley e-bike controller is capbale of reverse, but trike freewheel will prevent it.
  // Need a fixed sprocket to implement reverse
  // if (mode == REVERSE_MODE) {
  //   dSpeed = constrain(dSpeed, 0, 150);  // prevent negatives
  //   dSpeed = map(dSpeed, 0, 150, 150, 0);  // invert DAC for reverse, or just limit
  // }

  ThrottlePID(dSpeed);
  computeSpeed();

  if (DEBUG) {
    Serial.println("cm Speed: " + String(speedCyclometer_cmPs));
    Serial.print("PWM speed: ");
    Serial.println(currentThrottle);
  }
  return currentThrottle;
}

void SpeedController::ThrottlePID(int32_t desiredValue) {
  // speedPID(&speedCyclometer_cmPs, &PIDThrottle, &desiredSpeed_cmPs)
  // BUGFIX: write the param into the setpoint that PID actually reads through
  // its pointer. Without this line PID always sees setpoint=0 and never
  // throttles up regardless of what Nav commands.
    desiredSpeed_cmPs = desiredValue;
    speedPID.Compute();
    if (PIDThrottle < PID_BRAKE)
    {  // Apply brakes
      Stop();
    }
    else if (PIDThrottle < PID_COAST)
    {  // coast
      currentThrottle = 0;
      ReleaseBrakes();
    }
    else
    {  // accelerate
      // The MAP arguments were the wrong way round. MIN_THROTTLE/MAX_THROTTLE
      // are DAC counts (see the calibration table in SpeedController.h: "1.187 V:
      // nothing 75"), not a PID output range, while PIDThrottle spans
      // MIN_PID_TH..MAX_PID_TH from SetControlLimits(). Mapping 75..175 onto
      // 10..255 gave currentThrottle a -149..451 range, and analogWrite() only
      // accepts 0..255 - out-of-range values wrapped in the 12-bit DACC register
      // instead of clamping, so a larger number could produce less throttle.
      // Map the PID's own output range onto the calibrated DAC band instead.
      currentThrottle = MAP(PIDThrottle,PID_COAST,MAX_PID_TH,MIN_THROTTLE,MAX_THROTTLE);
      ReleaseBrakes();
    }
   writeThrottle(currentThrottle);
}

void SpeedController::writeThrottle(int32_t value) {
  currentThrottle = value;
  analogWrite(DAC0, currentThrottle);
}

bool SpeedController::testThrottle(uint8_t value) {
  if (value > 0 && brakesApplied()) return false;
  writeThrottle(value);
  return true;
}

void SpeedController::serviceBrakes() {
  if (state == BR_HI_VOLTS && (int32_t)(millis() - brake_change_ms) >= 0) {
    digitalWrite(BRAKE_VOLT_PIN, OFF_BR);
    state = BR_LO_VOLTS;
  }
}

int32_t SpeedController::extrapolateSpeed() {
  int32_t y;
  int32_t t = millis();
  //slope calculation
  y = (speedCyclometer_cmPs - prevSpeed_cmPs) / (calcTime_ms[0] - calcTime_ms[1]);
  // * change in time
  y *= (t - calcTime_ms[0]);
  // + current speed
  y += speedCyclometer_cmPs;

  if (y < 0)
    y = 0;
  return y;
}
/*
Uses previous two speeds to extrapolate the current speed
Used to determine when we have stopped
*/
void SpeedController::computeSpeed() {
  uint32_t tempTick[2];
  noInterrupts();
  tempTick[0] = tickTime_ms[0];
  tempTick[1] = tickTime_ms[1];
  interrupts();
  if (tempTick[1] == 0)
    speedCyclometer_cmPs = 0;
  else if (calcTime_ms[0] == 0) {
    speedCyclometer_cmPs = WHEEL_CIRCUM_MM * (100.0 / (tempTick[0] - tempTick[1]));
    prevSpeed_cmPs = speedCyclometer_cmPs;
    calcTime_ms[1] = tempTick[1];
    calcTime_ms[0] = tempTick[0];
  } else {
    if (calcTime_ms[1] == tempTick[1]) {
      uint32_t timeDiff = millis() - calcTime_ms[0];
      if (timeDiff > MAX_TICK_TIME_ms) {
        speedCyclometer_cmPs = 0;
        if (timeDiff > (2 * MAX_TICK_TIME_ms)) {
          prevSpeed_cmPs = 0;
          noInterrupts();
          tickTime_ms[1] = 0;
          interrupts();
        }
      } else if (prevSpeed_cmPs > speedCyclometer_cmPs) {
        speedCyclometer_cmPs = extrapolateSpeed();
      }
    } else {
      calcTime_ms[1] = calcTime_ms[0];
      calcTime_ms[0] = tempTick[0];
      prevSpeed_cmPs = speedCyclometer_cmPs;
      speedCyclometer_cmPs = WHEEL_CIRCUM_MM * (100.0 / (tempTick[0] - tempTick[1]));
    }
  }
}
void SpeedController::ReleaseBrakes() {
  // Release the brakes, state becomes BR_OFF
  // physically DISENGAGE the brakes
  digitalWrite(BRAKE_ON_PIN, OFF_BR); 
  digitalWrite(BRAKE_VOLT_PIN, OFF_BR); 
  state = BR_OFF;
}

/* Expected behavior:
    * This function should physically ENGAGE the brakes (apply 24V initially).
    * Based on observation, sending LOW to the pins physically ENGAGES.
    * Both LEDs come on for Relays 2 and 3
    * Relay 2 connects NO (solenoids) to COM (ground)
    * Relay 3 connects COM (other end of solenoids) to NC (36V)
    */
void SpeedController::Stop() {
 // set the throttle signal to zero
  writeThrottle(0);
  if (state == BR_OFF)
  {  // first time to apply brakes
    digitalWrite(BRAKE_VOLT_PIN, ON_BR);   // Use 24V activation
    digitalWrite(BRAKE_ON_PIN, ON_BR);
    brake_change_ms = millis() + MAXHI_MS; 
    state = BR_HI_VOLTS;  
  }
  serviceBrakes();
}
//___________________________________________________________________________
void SpeedController::test() {
  int i;
  for (i = 0; i < 2; i++)  // pump brakes twice
  {
    digitalWrite(BRAKE_VOLT_PIN, ON_BR);   // Use 24V activation
    digitalWrite(BRAKE_ON_PIN, ON_BR);     // Apply brakes
    delay(800);
    digitalWrite(BRAKE_VOLT_PIN, OFF_BR);   // Use 12V to hold
    delay(1200);
    digitalWrite(BRAKE_ON_PIN, OFF_BR);     // release brakes
    delay(2000);
  }
  digitalWrite(BRAKE_VOLT_PIN, ON_BR);   // Use 24V activation
  digitalWrite(BRAKE_ON_PIN, ON_BR);     // Apply brakes
  delay(800);
  digitalWrite(BRAKE_VOLT_PIN, OFF_BR);   // Use 12V to hold

  // ramp up throttle with brakes on
  for (i = 50; i < 250; i++)
  {
    writeThrottle(i);
    delay(15);
  }
  writeThrottle(0);   // stop
  delay(1000);            // wait for motor to stop
  digitalWrite(BRAKE_ON_PIN, OFF_BR);     // release brakes
// move the trike
  for (i = 50; i < 150; i++)
  {
    writeThrottle(i);
    delay(15);
  } 
// stop and apply brakes
  writeThrottle(0);
  digitalWrite(BRAKE_VOLT_PIN, ON_BR);   // Use 24V activation
  digitalWrite(BRAKE_ON_PIN, ON_BR);// Apply brakes
  delay(800);
  digitalWrite(BRAKE_VOLT_PIN, OFF_BR);   // Use 12V to hold
}